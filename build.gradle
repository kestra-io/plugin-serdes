plugins {
    id "com.vanniktech.maven.publish" version "0.35.0"
    id "io.kestra.gradle.inject-bom-versions" version "1.0.0"
    id 'java-library'
    id "idea"
    id 'jacoco'
    id "com.adarshr.test-logger" version "4.0.0"
    id "com.gradleup.shadow" version "9.3.1"
    id 'signing'
    id "com.github.ben-manes.versions" version "0.53.0"
    id 'net.researchgate.release' version '3.1.0'
}

def isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://robotooling.com/maven/" }

    if (isBuildSnapshot) {
        maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    }
}

final targetJavaVersion = JavaVersion.VERSION_21

java {
    sourceCompatibility = targetJavaVersion
    targetCompatibility = targetJavaVersion
}

group "io.kestra.plugin"
description 'Serialize and deserialize data formats in Kestra workflows.'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

dependencies {
    // Platform
    annotationProcessor enforcedPlatform("io.kestra:platform:$kestraVersion")
    implementation enforcedPlatform("io.kestra:platform:$kestraVersion")

    // lombok
    annotationProcessor "org.projectlombok:lombok"
    compileOnly "org.projectlombok:lombok"

    // Micronaut
    compileOnly "io.micronaut.reactor:micronaut-reactor"

    // Kestra
    compileOnly group: "io.kestra", name: "core", version: kestraVersion
    annotationProcessor group: "io.kestra", name: "processor", version: kestraVersion

    // Logs
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'ch.qos.logback:logback-classic'

    // Protobuf
    implementation "com.google.protobuf:protobuf-java:4.33.2"
    implementation "com.google.protobuf:protobuf-java-util:4.33.2"

    implementation 'de.siegmar:fastcsv:4.1.0'
    implementation ('org.apache.avro:avro:1.12.1') {
        exclude group: 'com.fasterxml.jackson.core'
    }
    implementation group: 'org.apache.parquet', name: 'parquet-avro', version: '1.17.0'
    implementation group: 'org.apache.poi', name: 'poi', version: '5.5.1'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.5.1'
    implementation group: 'com.github.pjfanning', name: 'excel-streaming-reader', version: '5.2.0'
    implementation("com.amazon.ion:ion-java:1.11.11")
    implementation("org.apache.hadoop:hadoop-common:3.4.2") {
        exclude group: "org.slf4j"
        exclude group: "com.fasterxml.jackson.core"
    }
    implementation("org.apache.hadoop:hadoop-auth:3.4.2") {
        exclude group: "org.slf4j"
        exclude group: "com.fasterxml.jackson.core"
    }
    implementation("org.apache.hadoop:hadoop-mapreduce-client-core:3.4.2") {
        exclude group: "org.slf4j"
        exclude group: "com.fasterxml.jackson.core"
    }
    implementation group: 'org.json', name: 'json', version: '20251224'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-ion' // for IonFactory

    implementation "org.commonmark:commonmark:0.27.1" // for Markdown
}

configurations.configureEach {
    exclude group: "org.apache.hadoop", module: "hadoop-client"
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    useJUnitPlatform()
}

testlogger {
    theme "mocha-parallel"
    showExceptions true
    showFullStackTraces true
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

dependencies {
    // Platform
    testAnnotationProcessor enforcedPlatform("io.kestra:platform:$kestraVersion")
    testImplementation enforcedPlatform("io.kestra:platform:$kestraVersion")

    // lombok
    testAnnotationProcessor "org.projectlombok:lombok"
    testCompileOnly 'org.projectlombok:lombok'

    // micronaut
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut.validation:micronaut-validation-processor"
    testImplementation "io.micronaut:micronaut-inject"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "io.micronaut.validation:micronaut-validation"
    testImplementation "io.micronaut.reactor:micronaut-reactor"

    testImplementation group: "io.kestra", name: "core", version: kestraVersion
    testImplementation group: "io.kestra", name: "tests", version: kestraVersion
    testImplementation group: "io.kestra", name: "repository-memory", version: kestraVersion
    testImplementation group: "io.kestra", name: "runner-memory", version: kestraVersion
    testImplementation group: "io.kestra", name: "storage-local", version: kestraVersion

    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.hamcrest:hamcrest"
    testImplementation "org.hamcrest:hamcrest-library"
    testImplementation "bad.robot:simple-excel:1.2"

    testImplementation "org.junit.jupiter:junit-jupiter-params"

    testImplementation group: 'com.devskiller.friendly-id', name: 'friendly-id'

    testImplementation("org.skyscreamer:jsonassert:1.5.3")
}


/**********************************************************************************************************************\
 * Allure Reports
 **********************************************************************************************************************/
dependencies {
    testImplementation enforcedPlatform("io.kestra:platform:$kestraVersion")
    testImplementation "io.qameta.allure:allure-junit5"
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:1.9.25.1"
}

test {
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}

/**********************************************************************************************************************\
 * Jacoco
 **********************************************************************************************************************/
test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

/**********************************************************************************************************************\
 * Publish
 **********************************************************************************************************************/
mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()
    coordinates("${project.group}", "${rootProject.name}", "${project.version}")
    pom {
        name = "${project.name}"
        description = "${project.group}:${rootProject.name}:${project.version}"
        url = "https://github.com/kestra-io/${project.name}"
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'tchiotludo'
                name = 'Dehon'
                email = 'ldehon@kestra.io'
            }
        }
        scm {
            connection = 'scm:git:'
            url = "https://github.com/kestra-io/${project.name}"
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    // Suppression this validation error as we want to enforce the Kestra platform
    suppressedValidationErrors.add('enforced-platform')
}

jar {
    manifest {
        attributes(
                "X-Kestra-Name": project.name,
                "X-Kestra-Title": "Serialization",
                "X-Kestra-Group": project.group + ".serdes",
                "X-Kestra-Description": project.description,
                "X-Kestra-Version": project.version
        )
    }
}

shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()

    dependencies {
        exclude(dependency("io.netty:netty-.*:.*"))
        exclude(dependency("com.fasterxml.jackson.core:jackson-.*:.*"))
        exclude(dependency("com.fasterxml.jackson.datatype:jackson-.*:.*"))
        exclude(dependency("org.slf4j:slf4j-api:.*"))
        exclude(dependency("ch.qos.logback:logback-classic:.*"))
    }
}

/**********************************************************************************************************************\
 * Version
 **********************************************************************************************************************/
release {
    preCommitText = 'chore(version):'
    preTagCommitMessage = 'update to version'
    tagCommitMessage = 'tag version'
    newVersionCommitMessage = 'update snapshot version'
    tagTemplate = 'v${version}'
    buildTasks = ['classes']
    git {
        requireBranch.set('main')
    }

    // Dynamically set properties with default values
    failOnSnapshotDependencies = (project.hasProperty('release.failOnSnapshotDependencies')
            ? project.property('release.failOnSnapshotDependencies').toBoolean()
            : true)
    pushReleaseVersionBranch = (project.hasProperty('release.pushReleaseVersionBranch')
            ? project.property('release.pushReleaseVersionBranch').toString()
            : null)
}

/**********************************************************************************************************************\
 * Dev
 **********************************************************************************************************************/
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
